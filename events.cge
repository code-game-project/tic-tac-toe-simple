name tic_tac_toe_simple
version 0.4

// The 'mark' command can be sent to the server to mark a field with the player's sign.
command mark {
	row: int,
	column: int
}

// The 'start' event is sent to all players when the game starts.
event start {
	// A map of player IDs mapped to their respective signs.
	signs: map<sign>,
}

// The 'board' event is sent to all players when the board changes.
event board {
	// The board as rows of columns.
	board: list<list<field>>
}

// The 'invalid_action' event notifies the player that their action was not allowed.
event invalid_action {
	// A message containing details on what the player did wrong.
	message: string
}

// The 'turn' event is sent to all players when it is the next player's turn.
event turn {
	// The sign of the player whose turn it is now.
	sign: sign
}

// The 'game_over' event is sent to all players when it's a tie or a player has won.
event game_over {
	// Whether it's a tie.
	tie: bool,
	// The sign of the winner.
	winner_sign: sign,
	// The three fields which form a row.
	winning_row: list<field>
}

// The two possible signs which can be placed on the board.
enum sign {
	none, x, o
}

// One of the nine fields on the board.
type field {
	row: int,
	column: int,
	sign: sign
}
